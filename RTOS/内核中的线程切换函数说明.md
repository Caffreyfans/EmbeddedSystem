# A9 内核中线程切换函数说明

## rt_hw_context_switch_to()

只有目标，没有来源线程。

- rt_hw_context_switch_to

```asm
/*
 * void rt_hw_context_switch_to(rt_uint32 to, struct rt_thread *to_thread);
 * r0 --> to (thread stack)
 * r1 --> to_thread
 */
 .globl rt_hw_context_switch_to
rt_hw_context_switch_to:
    ldr sp, [r0]           # 将 r0 中新线程的栈地址放入 sp 寄存器

    b       rt_hw_context_switch_exit # 跳转准备执行上下文切换
```

- rt_hw_context_switch_exit

```asm
.global rt_hw_context_switch_exit
rt_hw_context_switch_exit:

/* fpu context */
    ldmfd sp!, {r6}        # 将栈里的数据放入到 r6 寄存器中               
    vmsr fpexc, r6         # 将 r6 寄存器里的值放入到 fpexc 寄存器中
    tst  r6, #(1<<30)      # 判断 r6 中第 30 位是否为 1
    beq 1f                 # 如果不是 1，则跳转到 1f
    ldmfd sp!, {r5}        # 将栈中的数据 pop 到 r5 寄存器中
    vmsr fpscr, r5         # 将 r5 寄存器中的值放入 fpscr 寄存器中
    vldmia sp!, {d16-d31}  # 将栈中的数据 pop 到 d16-d31
    vldmia sp!, {d0-d15}   # 将栈中的数据 pop 到 d0-d15
1:

    ldmfd   sp!, {r1}                             # 将栈中的数据放入 r1 寄存器
    msr     spsr_cxsf, r1  /* original mode */    # 将 r1 的内容放入 spsr_scsf
    ldmfd   sp!, {r0-r12,lr,pc}^ /* irq return */ # 将栈中的数据 pop 回相应的寄存器
```

- rt_hw_context_switch

```asm
/*
 * void rt_hw_context_switch(rt_uint32 from, rt_uint32 to, struct rt_thread *to_thread);
 * r0 --> from (from_thread stack)
 * r1 --> to (to_thread stack)
 * r2 --> to_thread
 */
.globl rt_hw_context_switch
rt_hw_context_switch:
    stmfd   sp!, {lr}           # 将 Lr 中的值 push 到栈
    stmfd   sp!, {r0-r12, lr}   # 将相关寄存器的值 push 到栈

    mrs r4, cpsr                # 将 cpsr 中的值放入到 r4
    tst lr, #0x01               # 将 lr 中的值与 0x01 相与后查看状态
    orrne r4, r4, #0x20         # 如果结果不为 0，则将 r4 寄存器中的值或上 0x20

    stmfd sp!, {r4}             # 将 r4 的值 push 到栈  

    /* fpu context */
    vmrs r6, fpexc              # 将 fpexc 中的值放入 r6
    tst  r6, #(1<<30)           # 判断 r6 中的值第 30 位是否为 1
    beq 1f                      # 如果不是 1 跳转到 1f
    vstmdb sp!, {d0-d15}        # 将 d0-d15 寄存器 push 到栈
    vstmdb sp!, {d16-d31}       # 将 d16-d31 寄存器 push 到栈
    vmrs r5, fpscr              # 将 fpscr 中的值放入 r5
    stmfd sp!, {r5}             # 将 r5 中的值 push 到栈
1:
    stmfd sp!, {r6}             # 将 r6 中的值 push 到栈

    str sp, [r0]                # 将 sp 中的值放入 [r0] 的地址中
    ldr sp, [r1]                # 将 [r1] 地址中的值（也就是 to 线程的栈地址）放入 sp

    b       rt_hw_context_switch_exit
```



